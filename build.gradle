// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        versions = [
                kotlin           : '1.3.61',
                gradle           : '3.5.3',

                room             : '2.2.3',
                lifecycle        : '2.1.0',
                constraintLayout : '1.1.3',
                recycler_view    : '1.1.0',
                appcompat        : '1.1.0',

                material         : '1.0.0',
                dagger           : '2.25.3',
                rxJava           : '2.2.16',
                rxAndroid        : '2.1.1',
                cicerone         : '5.0.0'
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { subproject ->
    // Plugins
    apply plugin: subproject.name == "sample-app" ? 'com.android.application' : 'com.android.library'
    apply plugin: 'kotlin-android'
    if (subproject.name != "features-api") apply plugin: 'kotlin-kapt'
    if (subproject.name.contains("impl")) apply plugin: 'kotlin-android-extensions'

    android {
        compileSdkVersion 29
        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 29
            versionCode 1
            versionName "1.0"
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

        // AndroidX --- Recycler View
        implementation "androidx.recyclerview:recyclerview:$versions.recycler_view"

        // AndroidX --- AppCompat
        implementation "androidx.appcompat:appcompat:$versions.appcompat"

        // Rx --- RxJava + RxAndroid
        implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
        implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"

        if (subproject.name != "features-api") {
            // AndroidX --- Constraint Layout
            implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"

            // DI --- Dagger
            implementation "com.google.dagger:dagger:$versions.dagger"
            kapt "com.google.dagger:dagger-compiler:$versions.dagger"

            // Navigation --- Cicerone
            implementation "ru.terrakok.cicerone:cicerone:$versions.cicerone"

            if (subproject.name != "sample-app") {
                // AndroidX --- Room
                implementation "androidx.room:room-runtime:$versions.room"
                implementation "androidx.room:room-rxjava2:$versions.room"
                kapt "androidx.room:room-compiler:$versions.room"

                // AndroidX --- Lifecycle
                implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
                kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
            } else {
                // Project modules --- Feature implementations
                implementation project(':feature-diary-impl')
                implementation project(':feature-exercise-library-impl')
                implementation project(':feature-workout-library-impl')
            }

            if (subproject.name != "common") {
                // Material Design
                implementation "com.google.android.material:material:$versions.material"

                // Project modules --- Common module + Feature APIs
                implementation project(':common')
                implementation project(':features-api')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}